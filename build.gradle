buildscript {
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$KOTLIN_VERSION"
    }
}

plugins {
    id "org.jetbrains.intellij" version "0.0.42"
}

apply plugin: 'java'
apply plugin: 'kotlin'

sourceCompatibility = JAVA_VERSION

version = getVersionName()

repositories {
    jcenter()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-runtime:$KOTLIN_VERSION"
    compile "org.jetbrains.kotlin:kotlin-stdlib:$KOTLIN_VERSION"
    testCompile "org.assertj:assertj-core:2.2.0"
}

intellij {
    type = 'IU'
    version = INTELLIJ_VERSION
    downloadSources Boolean.valueOf(INTELLIJ_DOWNLOAD_SOURCES)

    pluginName = 'Ember.js'
    plugins = ['JavaScriptLanguage']
    updateSinceUntilBuild = false

    sandboxDirectory = project.rootDir.canonicalPath + "/.sandbox"

    publish {
        pluginId '8049'
        username "$JETBRAINS_USERNAME"
        password "$JETBRAINS_PASSWORD"
    }
}

test {
    testLogging {
        exceptionFormat = 'full'
    }
}

/*
 * Gets the version name from the latest Git tag
 */
def getVersionName() {
    def stdout = new ByteArrayOutputStream()
    try {
        exec {
            commandLine 'git', 'describe', '--tags'
            standardOutput = stdout
        }
    } catch (ignored) {
        return '0.0.0'
    }

    def version = stdout.toString().trim()
    if (version.startsWith('v'))
        version = version.substring(1)

    return version
}
